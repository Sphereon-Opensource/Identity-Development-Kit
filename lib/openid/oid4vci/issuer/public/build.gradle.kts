import com.sphereon.gradle.plugin.configureIosTargetsIfEnabled
import com.sphereon.gradle.plugin.configureLinuxTargetIfEnabled
import com.sphereon.gradle.plugin.configureWasmJsTargetIfEnabled
import org.jetbrains.kotlin.gradle.dsl.JsModuleKind

plugins {
    `maven-publish`
    alias(sphereonplug.plugins.org.jetbrains.kotlin.multiplatform)
    alias(sphereonplug.plugins.com.vanniktech.maven.publish)
    alias(sphereonplug.plugins.org.jetbrains.kotlin.plugin.serialization)
    alias(sphereonplug.plugins.com.sphereon.gradle.plugin.npm.publication)
    alias(sphereonplug.plugins.dev.zacsweers.metro)
}
metro {
}

kotlin {
    jvm()
    run {
        val kmpTargets = (System.getProperty("kmp.targets") ?: "jvm").split(",").map { it.trim().lowercase() }
        if ("all" in kmpTargets || "js" in kmpTargets) {
            js {
                compilerOptions {
                    moduleKind = JsModuleKind.MODULE_ES
                    target = "es2015"
                }
                browser { testTask { enabled = false } }
                nodejs { testTask { useMocha { timeout = "60000" } } }
                binaries.library()
                generateTypeScriptDefinitions()
            }
        }
    }
    configureWasmJsTargetIfEnabled {
        nodejs()
        binaries.library()
        generateTypeScriptDefinitions()
    }
    configureIosTargetsIfEnabled()
    configureLinuxTargetIfEnabled()

    sourceSets {
        findByName("jsTest")?.dependencies {
            implementation(sphereonlib.org.jetbrains.kotlinx.coroutines.core.js)
        }
        val commonMain by getting {
            dependencies {
                implementation(sphereonlib.co.touchlab.skie.configuration.annotations)
                api(projects.libCoreApiPublic)

                // OID4VCI protocol models
                api(projects.libOpenidOid4vciCommonPublic)

                // SD-JWT VC type-metadata model + the source-agnostic VCT builder, for the optional
                // VctTypeMetadataProvider SPI (issuer can serve VCTs from config or, later, a
                // semantic EDK/VDX source).
                api(projects.libSdjwtPublic)

                // OAuth2 common (for TokenResponse, AS metadata)
                api(projects.libOauth2CommonPublic)

                // Crypto (for managed identifiers, key types)
                api(projects.libCryptoCorePublic)

                // KV store (for session/nonce/offer persistence)
                api(projects.libDataStoreKvPublic)

                // Issuance pipeline configuration (for PipelineConfigurationResolver SPI)
                api(projects.libCredentialIssuancePipelinePublic)
                // StatusListBinding on IssuanceContext for OID4VCI status-list enrichment
                api(projects.libStatuslistPublic)

                // Serialization
                api(sphereonlib.org.jetbrains.kotlinx.serialization.json)

                api(libs.bundles.app.platform.di)
                api(sphereonlib.software.amazon.app.platform.metro.public)
            }
        }
        val commonTest by getting {
            dependencies {
                implementation(sphereonlib.org.jetbrains.kotlin.test)
                implementation(sphereonlib.org.jetbrains.kotlinx.coroutines.test)
            }
        }
    }
}
